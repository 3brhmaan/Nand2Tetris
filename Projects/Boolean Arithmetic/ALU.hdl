// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    // if (zx == 1) sets x = 0        // 16-bit constant
    Mux16(a=x , b=false , sel=zx , out=zX );

    // if (nx == 1) sets x = !x       // bitwise not
    Not16(in=zX , out=notzX );
    Mux16(a=zX , b=notzX , sel=nx , out=nX );

    // if (zy == 1) sets y = 0        // 16-bit constant
    Mux16(a=y , b=false , sel=zy , out=zY );

    // if (ny == 1) sets y = !y       // bitwise not
    Not16(in=zY , out=notzY );
    Mux16(a=zY , b=notzY , sel=ny , out=nY );

    // if (f == 1)  sets out = x + y  // integer 2's complement addition
    // if (f == 0)  sets out = x & y  // bitwise and
    Add16(a =nX , b =nY , out =xPlusy );
    And16(a=nX , b=nY , out=xAndy );
    Mux16(a=xAndy , b=xPlusy , sel=f , out=nOut );
    
    // if (no == 1) sets out = !out   // bitwise not
    // if (out < 0)  equals 1, else 0
    Not16(in=nOut , out=notNout );
    Mux16(a=nOut , b=notNout , sel=no , out=out , out[0..7]=zr1 , out[8..15]=zr2 , out[15]=ng );

    // if (out == 0) equals 1, else 0
    Or8Way(in=zr1 , out=out1 );
    Or8Way(in=zr2 , out=out2 );
    Or(a=out1 , b=out2 , out=zr1Orz2 );
    Not(in=zr1Orz2 , out=zr );
}